#compdef freebayes

_freebayes() {
	integer ret=1
	local info="-h --help --version"
	local -a args
	args+=(
		"(- *)"{-h,--help}'[Print help dialog]'
		"(- *)--version[Prints the release number and the git commit id]"
		"($info)*"{-b+,--bam=}'[Add FILE to the set of BAM files to be analyzed]:file:_files -g \*.bam'
		"($info)*"{-L+,--bam-list=}'[A file containing a list of BAM files to be analyzed]:file:_files'
		"($info -c --stdin)"{-c,--stdin}'[Read BAM input on stdin]'
		"($info -f --fasta-reference)"{-f+,--fasta-reference=}'[Use FILE as the reference sequence for analysis]:FILE:_files'
		"($info -t --targets)"{-t+,--targets=}'[Limit analysis to targets listed in the BED-format FILE]:FILE:_files -g \*.bed'
		"($info -r --region)"{-r+,--region=}'[Limit analysis to the specified region (same as BED format)]:region:'
		"($info -s --samples)"{-s+,--samples=}'[Limit analysis to samples listed (one per line) in the FILE]:FILE:_files'
		"($info)--populations=[Each line of FILE should list a sample and a population which it is part of]:FILE:_files"
		"($info -A --cnv-map)"{-A+,--cnv-map=}'[Read a copy number map from a BED file]:FILE:_files -g \*.bed'
		"($info -v --vcf)"{-v+,--vcf=}'[Output VCF-format results to FILE]:FILE:_files'
		"($info)--gvcf[Write gVCF output, which indicates coverage in uncalled regions]"
		"($info)--gvcf-chunk[When writing gVCF output emit a record for every NUM bases]"
		"($info -@ --variant-input)"{-@+,--variant-input=}'[Use variants reported in VCF file as input to the algorithm]:FILE:_files'
		"($info -l --only-use-input-alleles)"{-l,--only-use-input-alleles}'[Only provide variant calls and genotype likelihoods for sites and alleles which are provided in the VCF input]'
		"($info)--haplotype-basis-alleles[Only variant alleles provided in this input will be used for the construction of haplotype alleles]"
		"($info)--report-all-haplotype-alleles[Provide information about all alleles]"
		"($info)--report-monomorphic[Report even loci which appear to be monomorphic]"
		"($info -P --pvar)"{-P+,--pvar=}'[Report sites if the probability that there is a polymorphism at the site is greater than X]:num:'
		"($info)--strict-vcf[Generate strict VCF format (FORMAT/GQ will be an int)]"
		"($info -T --theta)"{-T+,--theta=}'[The expected mutation rate among the population under analysis]:num:'
		"($info -p --ploidy)"{-p+,--ploidy=}'[Sets the ploidy (default: 2)]:num:'
		"($info -J --pooled-discrete)"{-J,--pooled-discrete}'[Assume that samples result from pooled sequencing]'
		"($info -K --pooled-continuous)"{-K,--pooled-continuous}'[Output all alleles which pass input filters]'
		"($info -Z --use-reference-allele)"{-Z,--use-reference-allele}'[Includes the reference allele in the analysis as if it is another sample from the same population]'
		"($info)--reference-quality=[Mapping quality and base quality (default: 100,60)]:MQ,BQ:"
		"($info -I --no-snps)"{-I,--no-snps}"[Ignore SNP alleles]"
		"($info -i --no-indels)"{-i,--no-indels}"[Ignore insertion and deletion alleles]"
		"($info -X --no-mnps)"{-X,--no-mnps}"[Ignore multi-nuceotide polymorphisms, MNPs]"
		"($info -u --no-complex)"{-u,--no-complex}"[Ignore complex events (composites of other classes)]"
		"($info -n --use-best-n-alleles)"{-n+,--use-best-n-alleles=}"[Evaluate only the best N SNP alleles]:num:"
		"($info -E --max-complex-gap --haplotype-length)"{-E+,--max-complex-gap=,--haplotype-length=}'[Allow haplotype calls with contiguous embedded matches of up to length]:num:'
		"($info)--min-repeat-size=[Require the total repeat length at least this many bp]:num:"
		"($info)--min-repeat-entropy=[Build across sequence until it has entropy > N bits per bp]:num:"
		"($info)--no-partial-observations[Exclude observations which do not fully span the dynamically-determined detection window]"
		"($info -O --dont-left-align-indels)"{-O+,--dont-left-align-indels=}'[Turn off left-alignment of indels, which is enabled by default.]'
		"($info -4 --use-duplicate-reads)"{-4,--use-duplicate-reads}'[Include duplicate-marked alignments in the analysis.]'
		"($info -m --min-mapping-quality -0 --standard-filters)"{-m+,--min-mapping-quality=}'[Exclude alignments if they have a mapping quality less than Q]:num:'
		"($info -q --min-base-quality -0 --standard-filters)"{-q+,--min-base-quality=}'[Exclude alleles if their supporting base quality is less than Q]:num:'
		"($info -R --min-supporting-allele-qsum -0 --standard-filters)"{-R+,--min-supporting-allele-qsum=}'[Consider any allele in which the sum of qualities of supporting observations is at least Q]:num:'
		"($info -Y --min-supporting-mapping-qsum)"{-Y+,--min-supporting-mapping-qsum=}'[Consider any allele in which and the sum of mapping qualities of supporting reads is at least Q]:num:'
		"($info -Q --mismatch-base-quality-threshold)"{-Q+,--mismatch-base-quality-threshold=}'[Count mismatches toward --read-mismatch-limit if the base quality of the mismatch is >= Q]:num:'
		"($info -U --read-mismatch-limit)"{-U+,--read-mismatch-limit=}'[Exclude reads with more than N mismatches where each mismatch has base quality >= mismatch-base-quality-threshold.]:num:'
		"($info -z --read-max-mismatch-fraction)"{-z+,--read-max-mismatch-fraction=}'[Exclude reads with more than N fraction of mismatches where each mismatch has base quality >= mismatch-base-quality-threshold]:num:'
		"($info -$ --read-snp-limit)"{-$+,--read-snp-limit=}'[Exclude reads with more than N base mismatches, ignoring gaps with quality >= mismatch-base-quality-threshold]:num:'
		"($info -e --read-indel-limit)"{-e+,--read-indel-limit=}'[Exclude reads with more than N separate gaps]:num:'
		"($info -F --min-alternate-fraction)"{-F+,--min-alternate-fraction=}'[Require at least this fraction of observations supporting an alternate allele within a single individual]:num:'
		"($info -C --min-alternate-count)"{-C+,--min-alternate-count=}'[Require at least this count of observations supporting an alternate allele within a single individual]:num:'
		"($info -3 --min-alternate-qsum)"{-3+,--min-alternate-qsum=}'[Require at least this sum of quality of observations supporting an alternate allele within a single individua]:num:'
		"($info -G --min-alternate-total)"{-G+,--min-alternate-total=}'[Require at least this count of observations supporting an alternate allele within the total population]:num:'
		"($info -0 --standard-filters)"{-0,--standard-filters}'[Use stringent input base and mapping quality filters]'
		"($info)--min-coverage=[Require at least this coverage to process a site]:num"
		"($info)--max-coverage=[Do not process sites with greater than this coverage]:num"
		"($info -k --no-population-priors)"{-k,--no-population-priors}'[Equivalent to --pooled-discrete --hwe-priors-off and removal of Ewens Sampling Formula component of priors]'
		"($info -w --hwe-priors-off)"{-w,--hwe-priors-off}'[Disable estimation of the probability of the combination arising under HWE]'
		"($info -V --binomial-obs-priors-off)"{-V,--binomial-obs-priors-off}'[Disable incorporation of prior expectations about observations]'
		"($info -a --allele-balance-priors-off)"{-a,--allele-balance-priors-off}'[Disable use of aggregate probability of observation balance between alleles]'
		"($info)--observation-bias=[Read length-dependent allele observation biases from FILE]:FILE:_files"
		"($info)--base-quality-cap=[Limit estimated observation quality by capping base quality at Q]:num:"
		"($info)--prob-contamination=[An estimate of contamination to use for all samples]:num:"
		"($info)--legacy-gls[Use legacy (polybayes equivalent) genotype likelihood calculations]"
		"($info)--contamination-estimates=[A file containing per-sample estimates of contamination]:FILE:_files"
		"($info)--report-genotype-likelihood-max[Report genotypes using the maximum-likelihood estimate]"
		"($info -B --genotyping-max-iterations)"{-B+,--genotyping-max-iterations=}'[Iterate no more than N times during genotyping step]:num:'
		"($info)--genotyping-max-banddepth=[Integrate no deeper than the Nth best genotype by likelihood]:num:"
		"($info _W --posterior-integration-limits)"{-W+,--posterior-integration-limits=}'[Integrate all genotype combinations in our posterior space which include no more than N samples with their Mth best data likelihood]:N,M:'
		"($info -N --exclude-unobserved-genotypes)"{-N,--exclude-unobserved-genotypes}'[Skip sample genotypings for which the sample has no supporting reads]'
		"($info -S --genotype-variant-threshold)"{-S+,--genotype-variant-threshold=}'[Limit posterior integration]'
		"($info -j --use-mapping-quality)"{-j,--use-mapping-quality}'[Use mapping quality of alleles when calculating data likelihoods]'
		"($info -H --harmonic-indel-quality)"{-H,--harmonic-indel-quality}'[Use a weighted sum of base qualities around an indel]'
		"($info -D --read-dependence-factor)"{-D+,--read-dependence-factor=}'[Incorporate non-independence of reads by scaling successive observations]:num:'
		"($info -\= --genotype-qualities)"{-\\\=,--genotype-qualities}'[Calculate the marginal probability of genotypes and report as GQ]'
		"($info -d --debug)*"{-d,--debug}'[Print debugging output]'
	)
	_arguments -w -s -S $args[@] '*:file:_files' && ret=0
	return ret
}

_freebayes
